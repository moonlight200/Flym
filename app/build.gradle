plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    // TODO remove
    id "kotlin-android-extensions"
    id "dagger.hilt.android.plugin"
}

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'

    defaultConfig {
        applicationId "net.frju.flym"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 40
        versionName "2.5.6"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        testInstrumentationRunner = "wtf.moonlight.flym.HiltTestRunner"
        //testApplicationId = "wtf.moonlight.flym.test"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11

        kotlinOptions {
            jvmTarget = "11"
        }
    }

    lintOptions {
        // if true, stop the gradle build if errors are found
        //abortOnError true
    }

    File localProps = project.rootProject.file('local.properties')
    Properties properties = new Properties()
    if (localProps.exists()) {
        properties.load(localProps.newDataInputStream())
    }

    signingConfigs {
        release {
            if (properties.getProperty("KEYSTORE_PATH") != null) {
                storeFile file(properties.getProperty("KEYSTORE_PATH"))
                storePassword properties.getProperty("KEYSTORE_PASSWORD")
                keyAlias properties.getProperty("KEY_ALIAS")
                keyPassword properties.getProperty("KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (properties.getProperty("KEYSTORE_PATH") != null) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            //applicationIdSuffix '.debug'
            versionNameSuffix ' (Debug)'
            debuggable true
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion '1.5.10'
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

kapt {
    arguments {
        arg("room.schemaLocation", "$projectDir/schemas")
    }
    correctErrorTypes = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttp_version"

    // Rome tools
    implementation "com.rometools:rome:$rome_version"
    implementation "com.rometools:rome-opml:$rome_version"

    // Room database
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Work manager
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Dagger Hilt dependency injection
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "androidx.hilt:hilt-work:$hilt_work_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:$hilt_work_version"

    // Timber
    implementation "com.jakewharton.timber:timber:4.7.1"

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    // TODO remove, replaced with compose component
    implementation 'androidx.cardview:cardview:1.0.0'
    // TODO remove, replaced with compose component
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    // TODO remove anko
    def anko_version = '0.10.8'
    implementation "org.jetbrains.anko:anko-sdk21:$anko_version"
    implementation "org.jetbrains.anko:anko-sdk21-listeners:$anko_version"
    implementation "org.jetbrains.anko:anko-appcompat-v7:$anko_version"
    implementation "org.jetbrains.anko:anko-design:$anko_version"

    def glide_version = '4.12.0'
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation 'androidx.activity:activity-compose:1.3.1'

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.paging:paging-runtime-ktx:3.0.1'
    // TODO remove, replaced with compose component
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'org.jsoup:jsoup:1.14.2'
    // TODO remove, replaced with compose component
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    implementation 'com.github.Tunous:SwipeActionView:1.3.1'
    // TODO remove, replaced with compose component
    implementation 'com.bignerdranch.android:expandablerecyclerview:3.0.0-RC1'
    // TODO remove
    implementation 'com.github.jrvansuita:MaterialAbout:0.2.3'
    implementation 'q.rorbin:badgeview:1.1.3'
    implementation 'net.dankito.readability4j:readability4j:1.0.6'
    implementation 'pub.devrel:easypermissions:3.0.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptTest "com.google.dagger:hilt-compiler:$hilt_version"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "com.google.truth:truth:1.1.3"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
}
